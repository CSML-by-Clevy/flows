start:
  say Typing(1000)
  say "Hello, I am your assistant to browse through Github."
  goto searchOrTrending

searchOrTrending:
  use Button("Trending üåü") as btntrending
  use Button("Show me my last search") as btnlast
  use Button("New search query") as btnnew

  say Typing(500)
  say Question(
    title = "Do you want to check out the trending repos or search for specific ones?",
    buttons = [btntrending, btnlast, btnnew]
  )
  hold

  if (event match btntrending) goto trending
  else if (event match btnlast) goto lastSearch
  else if (event match btnnew) goto query
  goto searchOrTrending

trending:
  use Button("Last month") as btnmonth
  use Button("Last week") as btnweek

  say Typing(500)
  say Question(
    title = "Do you want me to show you the trending repos that were created over last month or last week?",
    buttons = [btnweek, btnmonth]
  )
  hold

  if (event match btnmonth) {
    say "Here are last month's trending repos:"
    remember "month" as period
  }
  else if (event match btnweek) {
    say "Here are last week's trending repos:"
    remember "week" as period
  }
  else {
    say "I'm sorry, I didn't get that"
    goto trending
  }

  use Fn("Github", action="trending", period=period) as queryResult
  foreach (repo) in queryResult {
    say Typing(2000)
    say "[{{repo.full_name}}](repo.html_url) - ‚≠êÔ∏è{{repo.stargazers_count}} ‚≠êÔ∏è - {{repo.language}} - {{repo.description}}"
  }
  goto end

lastSearch:
  if (githubSearchLanguage) goto resultSearch
  else {
    say "It is actually the first time we talk so tell me what you need."
    goto query
  }

query:
  say Typing(1000)
  say "Whats your query?"
  hold

  remember event as githubSearchQuery
  goto language

language:
  say Typing(1000)
  say "Which language?"
  hold

  remember event as githubSearchLanguage
  goto resultSearch

resultSearch:
  say Typing(2000)
  say "Here are the results we found for for you:"
  use Fn("Github", action="search", query=githubSearchQuery, language=githubSearchLanguage) as queryResult
  foreach (repo) in queryResult {
    say Typing(2000)
    say "[{{repo.full_name}}](repo.html_url) - {{repo.description}}"
  }
  goto end

